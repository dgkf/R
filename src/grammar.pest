//
// Since newlines are sometimes meaningful syntax, we need to specify what 
// whitespace is permitted where. 
//
// This might be easier in future versions of `pest`, though the addition of 
// this feature has not moved much since the middle of 2022:
// https://github.com/pest-parser/pest/issues/271
//

// whitespace helpers

    WS_NO_NL = _{ " " | "\\t" }
    WS = _{ WS_NO_NL | NEWLINE }
    WB = _{ eoi | !ASCII_ALPHA } // boundary
    CAPTURE_WS = { WS+ }
    eoi  = _{ !ANY }  // since EOI does not parse silently, define our own
    

// highlighting
//
// Syntax highlighting uses a parallel grammar (trying to reuse as much as 
// possible), that tries to be minimally recursive. In the future this might 
// shift to using something like a treesitter grammar, but for now this is 
// just a minimal product for building the infrastructure for highlighting
// into the repl
//

    hl = _{ CAPTURE_WS? ~ ( hl_kws  ~ CAPTURE_WS? )* ~ eoi }
        hl_kws = _{ hl_infix | hl_reserved | hl_control | hl_call | hl_value | hl_num | hl_sym | hl_str | hl_ops | hl_brackets | hl_comment | hl_other }
        hl_comment = { comment }
        hl_control = { hl_control_kws ~ WB }
        hl_control_kws = _{ "if" | "else" | "for" | "while" | "repeat" | "return" }
        hl_reserved = { hl_reserved_kws ~ WB }
        hl_reserved_kws = _{ "function" | "fn" }
        hl_value = { hl_value_kws ~ WB }
        hl_value_kws = _{ val_null | val_na | val_inf | val_true | val_false }
        hl_call = _{ hl_callname ~ CAPTURE_WS* ~ hl_open }
        hl_callname = { hl_sym | hl_str }
        hl_sym = { hl_symbol_backticked | symbol_ident }
        hl_symbol_backticked = ${ "`" ~ (!"`" ~ ANY)* ~ ( "`" | eoi ) }
        hl_str = ${ "\"" ~ double_quoted_string ~ ( "\"" | eoi ) | "'" ~ single_quoted_string ~ ( "'" | eoi ) }
        hl_num = { number ~ "L"? }
        hl_infix = { infix }
        hl_open = { "(" }
        hl_brackets = { hl_open | ")" | "[" | "]" | "{" | "}" }
        hl_ops = { "," | "+" | "-" | "*" | "/" | "<" | ">" | "=" | "&" | "!" | "^" | ":" | "::" | ":::" | "?" }
        hl_other = { ANY }


// repl line feed parsing

    repl = _{ WS* ~ expr ~ WS* ~ comment? ~ eoi | WS* ~ block_exprs ~ WS* ~ eoi }

// expression basics

    expr = { atomic ~ ( WS* ~  infix ~ WS* ~ atomic )* }

        comment = _{ "#" ~ ( !NEWLINE ~ !eoi ~ ANY )* }

        atomic = _{ prefixed }
            prefixed = { prefix* ~ postfixed }
            postfixed = { atom ~ postfix* }

        infix = _{ 
                assign | 
                add | subtract | multiply | divide | modulo | power | 
                pipe | 
                colon | 
                gte | lte | gt | eq | neq | lt | 
                or | vor | and | vand | 
                special |
                dollar | 
                doublecolon | triplecolon
            }

            // numerics
            add = { "+" }
            subtract = { "-" }
            multiply = { "*" }
            divide = { "/" }
            modulo = { "%%" }
            power = { "^" }

            // comparison
            gt = { ">" }
            gte = { ">=" }
            eq = { "==" }
            neq = { "!=" }
            lt = { "<" }
            lte = { "<=" }
            or = { "||" }
            vor = { "|" }
            and = { "&&" }
            vand = { "&" }

            // assignment
            assign = { "<-" }

            // special
            special = { "%" ~ !("%") ~ "%" }
            pipe = { "|>" }
            dollar = { "$" }
            colon = { ":" }
            doublecolon = { "::" }
            triplecolon = { ":::" }

        prefix = _{ subtract | negate }
            negate = { "!" }

        postfix = _{ call | index | vector_index }
            call         = { "("  ~ pairs ~  ")" }
            index        = { "[[" ~ pairs ~ "]]" }
            vector_index = { "["  ~ pairs ~  "]" }           

        block  = _{ "{" ~ WS* ~ block_exprs ~ WS* ~ "}" }
            block_exprs = { WS* ~ expr? ~ ( block_sep ~ expr? )* ~ WS* }
            block_sep = _{ WS_NO_NL* ~ ( ";" | comment? ~ NEWLINE ) ~ WS* }

        paren_expr = _{ "(" ~ WS* ~ expr ~ WS* ~ ")" }

        atom = _{ 
              block
            | paren_expr
            | kw_function
            | kw_break
            | kw_continue
            | kw_if_else
            | kw_for
            | kw_while
            | kw_repeat
            | val_null
            | val_na
            | val_inf
            | val_true
            | val_false
            | integer_expr
            | string_expr 
            | number 
            | symbol 
            | list
            | vec
        }

// keyworded (kw) syntax

    kw_function_or_fn = _{ "function" | "fn" }
    kw_function = { kw_function_or_fn ~ WS* ~ list ~ WS* ~ expr }
    kw_if_else = { "if" ~ WS* ~ "(" ~ WS* ~ expr ~ WS* ~ ")" ~ WS* ~ expr ~ WS* ~ ("else" ~ WS* ~ expr)? }
    kw_for = { "for" ~ WS* ~ "(" ~ WS* ~ symbol ~ WS+ ~ "in" ~ WS+ ~ expr ~ WS* ~ ")" ~ WS* ~ expr }
    kw_while = { "while" ~ WS* ~ "(" ~ WS* ~ expr ~ WS* ~ ")" ~ WS* ~ expr }
    kw_repeat = { "repeat" ~ WS* ~ expr }
    kw_break = { "break" }
    kw_continue = { "continue" }

// reserved value (val) types

    val_null = { ( "NULL" | "null" ) ~ WB }
    val_na = { ( "NA" | "na" ) ~ WB }
    val_inf = { ( "Inf" | "inf" ) ~ WB }
    val_true = { ( "TRUE" | "true" ) ~ WB }
    val_false = { ( "FALSE" | "false" ) ~ WB }

// atomic value types

    number = @{ "-"? ~ (number_leading | number_trailing) }
        number_leading = { ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? }
        number_trailing = { "." ~ ASCII_DIGIT+ }

    integer_expr = _{ integer ~ "L" }
        integer = @{ "-"? ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }

    string_expr = _{ "\"" ~ double_quoted_string ~ "\"" | "'" ~ single_quoted_string ~ "'" }
        single_quoted_string = @{ single_quoted_string_char* }
        double_quoted_string = @{ double_quoted_string_char* }
        single_quoted_string_char = _{ !("'"  | "\\") ~ ANY | escaped_char }
        double_quoted_string_char = _{ !("\"" | "\\") ~ ANY | escaped_char }
        escaped_char = _{
              "\\" ~ ("\"" | "'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
            | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
        }

    symbol = _{ symbol_with_backticks | symbol_ident }
        symbol_with_backticks = _{ "`" ~ symbol_backticked ~ "`" }
        symbol_backticked = ${ ( !"`" ~ ANY )* }
        symbol_ident = ${ ( ASCII_ALPHA | "." | "_" ) ~ ( ASCII_ALPHANUMERIC | "." | "_" )* }

    list = { "(" ~ pairs ~ ")" }
        pairs = _{ ( ( WS* ~ elem? ~ WS* ~ "," )* ~ WS* ~ elem? )? ~ WS* }
        ellipsis = { "..." }
        elem = _{ ellipsis | named | expr }
        named = { symbol ~ WS* ~ "=" ~ WS* ~ expr? }

    vec = { "[" ~ pairs ~ "]" }
