//
// Since newlines are sometimes meaningful syntax, we need to specify what
// whitespace is permitted where.
//
// This might be easier in future versions of `pest`, though the addition of
// this feature has not moved much since the middle of 2022:
// https://github.com/pest-parser/pest/issues/271
//

// whitespace helpers

    WS_NO_NL = _{ " " | "\\t" }
    WS = _{ WS_NO_NL | NEWLINE }
    WB = _{ eoi | !ASCII_ALPHA } // boundary
    CAPTURE_WS = { WS+ }
    eoi  = _{ !ANY }  // since EOI does not parse silently, define our own


// highlighting
//
// Syntax highlighting uses a parallel grammar (trying to reuse as much as
// possible), that tries to be minimally recursive. In the future this might
// shift to using something like a treesitter grammar, but for now this is
// just a minimal product for building the infrastructure for highlighting
// into the repl
//

    hl = _{ CAPTURE_WS? ~ ( hl_kws  ~ CAPTURE_WS? )* ~ eoi }
        hl_kws = _{ hl_infix | hl_function | hl_signal | hl_control | hl_call | hl_value | hl_num | hl_sym | hl_str | hl_ops | hl_brackets | hl_comment | hl_other }
        hl_comment = { comment }
        hl_control = { hl_control_kws ~ WB }
        hl_control_kws = _{ loc_else | loc_if | loc_in | loc_for | loc_while | loc_repeat }
        hl_signal = { hl_signal_kws ~ WB }
        hl_signal_kws = _{ loc_return | loc_break | loc_continue }
        hl_function = { hl_function_kws ~ WB }
        hl_function_kws = _{ loc_function | loc_fn }
        hl_value = { hl_value_kws ~ WB }
        hl_value_kws = _{ val_null | val_na | val_inf | val_true | val_false }
        hl_call = _{ hl_callname ~ CAPTURE_WS* ~ hl_open }
        hl_callname = { hl_sym | hl_str }
        hl_sym = { hl_symbol_backticked | symbol_ident }
        hl_symbol_backticked = ${ "`" ~ (!"`" ~ ANY)* ~ ( "`" | eoi ) }
        hl_str = ${ "\"" ~ double_quoted_string ~ ( "\"" | eoi ) | "'" ~ single_quoted_string ~ ( "'" | eoi ) }
        hl_num = { number ~ ("L" | "_")? }
        hl_infix = { infix }
        hl_open = { "(" }
        hl_brackets = { hl_open | ")" | "[" | "]" | "{" | "}" }
        hl_ops = { "," | "+" | "-" | "*" | "/" | "<" | ">" | "=" | "&" | "!" | "^" | ":" | "::" | ":::" | "?" }
        hl_other = { ANY }


// repl line feed parsing

    repl = _{ WS* ~ expr ~ WS* ~ comment? ~ eoi | WS* ~ block_exprs ~ WS* ~ eoi }

// expression basics

    expr = { atomic ~ ( WS* ~ infix ~ WS* ~ atomic )* }

        comment = _{ "#" ~ ( !NEWLINE ~ !eoi ~ ANY )* }

        atomic = _{ prefixed | standalone }
            prefixed = { prefix* ~ WS* ~ postfixed }
            postfixed = { atom ~ WS_NO_NL* ~ postfix* }

        infix = _{
                assign |
                add | subtract | multiply | divide | modulo | power |
                pipe |
                colon |
                gte | lte | gt | eq | neq | lt |
                or | vor | and | vand |
                special |
                dollar |
                doublecolon | triplecolon
            }

            // numerics
            add = { "+" }
            subtract = { "-" }
            multiply = { "*" }
            divide = { "/" }
            modulo = { "%%" }
            power = { "^" }

            // comparison
            gt = { ">" }
            gte = { ">=" }
            eq = { "==" }
            neq = { "!=" }
            lt = { "<" }
            lte = { "<=" }
            or = { "||" }
            vor = { "|" }
            and = { "&&" }
            vand = { "&" }

            // assignment
            assign = @{ "<-" | ("=" ~ !("="))}

            // special
            special = { "%" ~ !("%") ~ "%" }
            pipe = { "|>" }
            dollar = { "$" }
            colon = { ":" }
            doublecolon = { "::" }
            triplecolon = { ":::" }
            more = { ".." }

        prefix = _{ subtract | negate | more }
            negate = { "!" }

        postfix = _{ call | index | vector_index | more }
            call         = { "("  ~ pairs ~  ")" }
            index        = { "[[" ~ pairs ~ "]]" }
            vector_index = { "["  ~ pairs ~  "]" }

        standalone = _{ more }

        block  = _{ "{" ~ WS* ~ block_exprs ~ WS* ~ "}" }
            block_exprs = { WS* ~ expr? ~ ( block_sep+ ~ expr? )* ~ WS* }
            block_sep = _{ WS_NO_NL* ~ ( ";" | comment? ~ NEWLINE ) ~ WS* }

        paren_expr = { "(" ~ WS* ~ expr ~ WS* ~ ")" }

        atom = _{
              block
            | paren_expr
            | kw_function
            | kw_break
            | kw_continue
            | kw_return
            | kw_if_else
            | kw_for
            | kw_while
            | kw_repeat
            | val_null
            | val_na
            | val_inf
            | val_true
            | val_false
            | integer_expr
            | string_expr
            | number
            | symbol
            | list
            | vec
        }

// keyworded (kw) syntax

    kw_function_or_fn = _{ loc_function | loc_fn }
    kw_function = { kw_function_or_fn ~ WS* ~ list ~ WS* ~ expr }
    kw_if_else = { loc_if ~ WS* ~ "(" ~ WS* ~ expr ~ WS* ~ ")" ~ WS* ~ expr ~ ( WS* ~ loc_else ~ WS* ~ expr )? }
    kw_for = { loc_for ~ WS* ~ "(" ~ WS* ~ symbol ~ WS+ ~ loc_in ~ WS+ ~ expr ~ WS* ~ ")" ~ WS* ~ expr }
    kw_while = { loc_while ~ WS* ~ "(" ~ WS* ~ expr ~ WS* ~ ")" ~ WS* ~ expr }
    kw_repeat = { loc_repeat ~ WS* ~ expr }
    kw_break = { loc_break }
    kw_continue = { loc_continue }
    kw_return = { loc_return ~ WS* ~ expr }

// reserved value (val) types

    val_null = { ( loc_null ) ~ WB }
    val_na = { ( loc_na ) ~ WB }
    val_inf = { ( loc_inf ) ~ WB }
    val_true = { ( loc_true ) ~ WB }
    val_false = { ( loc_false ) ~ WB }

// atomic value types

    number = @{ number_leading | number_trailing }
        number_leading = { ("0" | ASCII_NONZERO_DIGIT ~ ("_"? ~ ASCII_DIGIT)*) ~ ("." ~ (ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)*))? }
        number_trailing = { "." ~ (ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)*) }

    integer_expr = _{ integer ~ "L" }
        integer = @{( ASCII_NONZERO_DIGIT ~ ("_"? ~ ASCII_DIGIT)* | "0" ) }

    string_expr = _{ "\"" ~ double_quoted_string ~ "\"" | "'" ~ single_quoted_string ~ "'" }
        single_quoted_string = @{ single_quoted_string_char* }
        double_quoted_string = @{ double_quoted_string_char* }
        single_quoted_string_char = _{ !("'"  | "\\") ~ ANY | escaped_char }
        double_quoted_string_char = _{ !("\"" | "\\") ~ ANY | escaped_char }
        escaped_char = _{
              "\\" ~ ("\"" | "'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
            | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
        }

    symbol = _{ symbol_with_backticks | symbol_ident }
        symbol_with_backticks = _{ "`" ~ symbol_backticked ~ "`" }
        symbol_backticked = ${ ( !"`" ~ ANY )* }
        symbol_ident = ${ ( LETTER | OTHER_SYMBOL | "_" | "." ) ~ ( ASCII_ALPHANUMERIC | "_" | "." )* }

    list = { "(" ~ pairs ~ ")" }
        pairs = _{ ( ( WS* ~ elem ~ WS* ~ "," )* ~ WS* ~ elem? )? ~ WS* }
        // ellipsis = { "..." }
        // elem = _{ ellipsis | named | expr }
        elem = _{ named | expr }
        named = { symbol ~ WS* ~ "=" ~ WS* ~ expr? }

    vec = { "[" ~ pairs ~ "]" }
